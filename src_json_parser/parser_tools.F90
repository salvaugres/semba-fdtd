module parser_tools_mod
   use labels_mod
   use mesh_mod
   use cells_mod
   use json_module
   use json_kinds
   use NFDETypes

   use, intrinsic :: iso_fortran_env , only: error_unit

   implicit none

   integer, private, parameter :: J_ERROR_NUMBER = 1

   type :: json_value_ptr
      type(json_value), pointer :: p
   end type

   type :: aux_node_t
      type(terminal_node_t) :: node
      integer :: cId
      type(coordinate_t) :: relPos
   end type

   type :: cable_ptr_t
      type(cable_t), pointer :: p
   end type
   
contains

   subroutine addCellRegionsAsCoords(res, cellRegions, cellType)
      type(coords), dimension(:), pointer :: res
      type(cell_region_t), dimension(:), intent(in) :: cellRegions
      integer, intent(in), optional :: cellType
      type(cell_interval_t), dimension(:), allocatable :: intervals
      type(coords), dimension(:), allocatable :: cs
      integer :: i

      allocate(intervals(0))
      do i = 1, size(cellRegions)
         if (present(cellType)) then
            intervals = [intervals, cellRegions(i)%getIntervalsOfType(cellType)]
         else
            intervals = [intervals, cellRegions(i)%intervals]
         end if
      end do
      cs = cellIntervalsToCoords(intervals)
      allocate(res(size(cs)))
      res = cs
   end subroutine

   subroutine addCellRegionsAsScaledCoords(res, cellRegions, cellType)
      type(coords_scaled), dimension(:), pointer :: res
      type(cell_region_t), dimension(:), intent(in) :: cellRegions
      integer, intent(in), optional :: cellType
      type(cell_interval_t), dimension(:), allocatable :: intervals
      type(coords), dimension(:), allocatable :: cs
      integer :: i

      allocate(intervals(0))
      do i = 1, size(cellRegions)
         if (present(cellType)) then
            intervals = [intervals, cellRegions(i)%getIntervalsOfType(cellType)]
         else
            intervals = [intervals, cellRegions(i)%intervals]
         end if
      end do

      if (any(intervals%getType() /= CELL_TYPE_LINEL)) then
         stop "Error converting cell regions to scaled coordinates. Only linels are supported."
      end if

      cs = cellIntervalsToCoords(intervals)

      allocate(res(size(cs)))
      res(:)%Xi = cs(:)%Xi
      res(:)%Xe = cs(:)%Xe
      res(:)%Yi = cs(:)%Yi
      res(:)%Ye = cs(:)%Ye
      res(:)%Zi = cs(:)%Zi
      res(:)%Ze = cs(:)%Ze
      res(:)%Or = cs(:)%Or
      res(:)%tag = cs(:)%tag

      res(:)%xc = 0.0
      res(:)%yc = 0.0
      res(:)%zc = 0.0
      do i = 1, size(cs)
         select case (cs(i)%Or)
          case (iEx)
            res(i)%xc = 1.0
          case (-iEx)
            res(i)%xc = -1.0
          case (iEy)
            res(i)%yc = 1.0
          case (-iEy)
            res(i)%yc = -1.0
          case (iEz)
            res(i)%zc = 1.0
          case (-iEz)
            res(i)%zc = -1.0
         end select
      end do

   end subroutine

   function cellIntervalsToCoords(ivls) result(res)
      type(coords), dimension(:), allocatable :: res
      type(cell_interval_t), dimension(:), intent(in) :: ivls
      integer :: i

      allocate(res(size(ivls)))
      do i = 1, size(ivls)
         res(i)%Or = ivls(i)%getOrientation()
         call convertInterval(res(i)%Xi, res(i)%Xe, ivls(i), DIR_X)
         call convertInterval(res(i)%Yi, res(i)%Ye, ivls(i), DIR_Y)
         call convertInterval(res(i)%Zi, res(i)%Ze, ivls(i), DIR_Z)
         res(i)%tag = ''
      end do
   contains
      subroutine convertInterval(xi, xe, interval, dir)
         integer, intent(out) :: xi, xe
         type(cell_interval_t), intent(in) :: interval
         integer, intent(in) :: dir
         integer :: a, b
         a = interval%ini%cell(dir) 
         b = interval%end%cell(dir)
         if (a < b) then
            xi = a
            xe = b - 1
         else if (a == b) then
            xi = a
            xe = b
         else
            xi = b + 1
            xe = a - 1
         end if

      end subroutine
   end function

   function getPixelFromElementId(mesh, id) result(res)
      type(pixel_t) :: res
      type(mesh_t), intent(in) :: mesh
      integer, intent(in) :: id

      type(node_t) :: node
      logical :: nodeFound
      logical :: cellRegionFound
      integer :: i

      node = mesh%getNode(id, nodeFound)
      if (nodeFound) then
         res = mesh%convertNodeToPixel(node)
      else
         stop "Error converting pixel. Node not found."
      end if

   end function

   function vectorToDiagonalMatrix(vector) result(res)
      real, dimension(:), intent(in) :: vector
      real, dimension(:, :), allocatable :: res
      integer :: i, n
      n = size(vector, 1)
      allocate(res(n,n), source = 0.0)
      do i = 1, n
         res(i,i) = vector(i)
      end do
   end function

   function scalarToMatrix(scalar) result(res)
      real, intent(in) :: scalar
      real, dimension(:, :), allocatable :: res
      allocate(res(1,1), source = 0.0)
      res(1,1) = scalar
   end function

end module
